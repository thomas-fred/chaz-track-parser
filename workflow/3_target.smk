rule parse_ssp:
    """
    Generate all tracks for a given SSP

    Test with:
    snakemake -c1 data/out/genesis-CRH/SSP-585.flag
    """
    input:
        expand(
            "{{data}}/out/genesis-{{genesis}}/SSP-{{ssp}}/GCM-{gcm}/epoch-{epoch}/norm-freq",
            gcm=GCMS,
            epoch=EPOCHS,
        )
    output:
        "{data}/out/genesis-{genesis}/SSP-{ssp}.flag"
    shell:
        "touch {output}"


rule parse_genesis_method:
    """
    Generate all tracks for a given genesis method

    Test with:
    snakemake -c1 data/out/genesis-CRH.flag
    """
    input:
        expand(
            "{{data}}/out/genesis-{{genesis}}/SSP-{ssp}/GCM-{gcm}/epoch-{epoch}/norm-freq",
            ssp=SSPS,
            gcm=GCMS,
            epoch=EPOCHS,
        )
    output:
        "{data}/out/genesis-{genesis}.flag"
    shell:
        "touch {output}"


rule parse_all:
    """
    Generate all tracks for all genesis methods

    Test with:
    snakemake -c1 data/out.flag
    """
    input:
        expand(
            "{{data}}/out/genesis-{genesis}/SSP-{ssp}/GCM-{gcm}/epoch-{epoch}/norm-freq",
            genesis=GENESIS_METHODS,
            ssp=SSPS,
            gcm=GCMS,
            epoch=EPOCHS,
        )
    output:
        "{data}/out.flag"
    shell:
        "touch {output}"


def gather_all_samples(wildcards):
    """
    We don't know ahead of time how many complete millennia of TC tracks each
    genesis-SSP-GCM-epoch will contain. This input function finds all those
    generated by expanding over the possible parameters and then globbing for
    all the existing samples (output millennia).

    """
    import glob
    to_glob = expand(
        f"{wildcards.data}" + "/out/genesis-{genesis}/SSP-{ssp}/GCM-{gcm}/epoch-{epoch}/norm-freq/sample-*/tracks.pq",
        genesis=GENESIS_METHODS,
        ssp=SSPS,
        gcm=GCMS,
        epoch=EPOCHS,
    )
    parquet = [path for glob_str in to_glob for path in glob.glob(glob_str)]
    geoparquet = [path.replace(".pq", ".gpq") for path in parquet]
    return geoparquet


checkpoint archive:
    """
    Collate all of the normalised epochal results.

    Test with:
    snakemake -c1 data/out/CHAZ-normalised-freq/
    """
    input:
        gather_all_samples
    threads: 4
    output:
        directory("{data}/out/CHAZ-normalised-freq/")
    shell:
        """
        mkdir -p {output}

        for INPUT_FILEPATH in {input}; do

            OUTPUT_FILENAME=CHAZ_$(echo $INPUT_FILEPATH | grep -o 'genesis-.*' | sed 's#/#_#g' | sed 's/_norm-freq//g' | sed 's/_tracks//g')

            cp $INPUT_FILEPATH {output}/$OUTPUT_FILENAME &

            # Run a maximum of `threads` concurrent copies
            while [ "$(jobs -rp | wc -l)" -ge "{threads}" ]; do
                sleep 0.1
            done

        done

        wait
        """

